{
  parserClass="com.warmthdawn.zenscript.parser.ZenScriptParser"
  parserUtilClass="com.warmthdawn.zenscript.parser.ZenScriptParserUtil"
  extends="com.intellij.extapi.psi.ASTWrapperPsiElement"

  psiClassPrefix="ZenScript"
  psiImplClassSuffix="Impl"
  psiPackage="com.warmthdawn.zenscript.psi"
  psiImplPackage="com.warmthdawn.zenscript.psi.impl"
  psiImplUtilClass="com.warmthdawn.zenscript.psi.ZenScriptImplUtil"

  elementTypeHolderClass="com.warmthdawn.zenscript.psi.ZenScriptTypes"
  elementTypeClass="com.warmthdawn.zenscript.psi.ZenScriptElementType"
  tokenTypeClass="com.warmthdawn.zenscript.psi.ZenScriptTokenType"
  
  tokens=[
    K_VAR                 ='var'
    K_VAL                 ='val'
    K_GLOBAL              ='global'
    K_STATIC              ='static'
    K_IMPORT              ='import'
    K_FUNCTION            ='function'
    K_AS                  ='as'
    K_TO                  ='to'
    K_IN                  ='in'
    K_HAS                 ='has'
    K_INSTANCEOF          ='instanceof'
    K_THIS                ='this'
    K_SUPER               ='super'

    K_ANY                 ='any'
    K_BYTE                ='byte'
    K_SHORT               ='short'
    K_INT                 ='int'
    K_LONG                ='long'
    K_FLOAT               ='float'
    K_DOUBLE              ='double'
    K_BOOL                ='bool'
    K_VOID                ='void'
    K_STRING              ='string'

    K_TRUE                ='true'
    K_FALSE               ='false'
    K_NULL                ='null'

    K_IF                  ='if'
    K_ELSE                ='else'
    K_FOR                 ='for'
    K_DO                  ='do'
    K_WHILE               ='while'
    K_BREAK               ='break'
    K_CONTINUE            ='continue'
    K_RETURN              ='return'
    K_ZEN_CLASS           ='zenClass'
    K_ZEN_CONSTRUCTOR     ='zenConstructor'
    K_EXPAND              ='$expand'

    // Separators
    PAREN_OPEN            ='('
    PAREN_CLOSE           =')'
    BRACK_OPEN            ='['
    BRACK_CLOSE           =']'
    BRACE_OPEN            ='{'
    BRACE_CLOSE           ='}'
    COMMA                 =','
    DOT                   ='.'
    SEMICOLON             =';'

    // Operators
    OP_ADD                ='+'
    OP_SUB                ='-'
    OP_MUL                ='*'
    OP_DIV                ='/'
    OP_MOD                ='%'
    OP_CAT                ='~'
    OP_NOT                ='!'
    OP_LESS               ='<'
    OP_GREATER            ='>'
    OP_XOR                ='^'
    OP_COLON              =':'
    OP_QUEST              ='?'
    OP_BACKTICK           ='`'
    OP_DOLLAR             ='$'
    OP_AND                ='&'
    OP_OR                 ='|'
    OP_ASSIGN             ='='

    OP_AND_AND            ='&&'
    OP_OR_OR              ='||'
    OP_EQUAL              ='=='
    OP_NOT_EQUAL          ='!='
    OP_LESS_EQUAL         ='<='
    OP_GREATER_EQUAL      ='>='
    OP_ADD_ASSIGN         ='+='
    OP_SUB_ASSIGN         ='-='
    OP_MUL_ASSIGN         ='*='
    OP_DIV_ASSIGN         ='/='
    OP_MOD_ASSIGN         ='%='
    OP_XOR_ASSIGN         ='^='
    OP_AND_ASSIGN         ='&='
    OP_OR_ASSIGN          ='|='
    OP_CAT_ASSIGN         ='~='
    OP_DOT_DOT            ='..'

    LINE_COMMENT          ='regexp://.*'
    BLOCK_COMMENT         ='BLOCK_COMMENT'
    DOC_COMMENT           ='DOC_COMMENT'
    PREPROCESSOR          ='regexp:#.*'

    ID                    ='regexp:[a-zA-Z_][a-zA-Z0-9_]*'
    STRING_LITERAL        ="regexp:('([^'\\]|\\.)*'|\"([^\"\\]|\\\"|'|\\)*\")"
    INT_LITERAL           ='regexp:(0|([1-9][0-9]*))'
    WHITE_SPACE           ='regexp:[ \r\t\f\n]+'

    NEW_LINE              ='<NEW_LINE>'
  ]

  extends("(mul|add|compare|and|xor|or|andAnd|orOr|assignment|range)Expression")=binaryExpression
  extends("[a-z][a-zA-Z]*Expression")=expression
  extends("[a-z][a-zA-Z]*Literal")=literalExpression
  extends("[a-z][a-zA-Z]*TypeRef")=typeRef
  extends("[a-z][a-zA-Z]*Statement")=statement
  implements("functionDeclaration|expandFunctionDeclaration|constructorDeclaration|functionLiteral")="ZenScriptFunction"

  generate=[
    token-sets="yes"
  ]

}


file ::= importList scriptBody

importList ::= importDeclaration*

// Declarations
importDeclaration ::= 'import' qualifiedName ('as' identifier)? ';'
    { pin(".*")=1; recoverWhile="import_recover";
    implements=["ZenScriptReference" "ZenScriptNamedElement" "com.intellij.psi.PsiPolyVariantReference"]; mixin="ZenScriptReferenceImpl";
    methods=[getIdentifier getName setName getNameIdentifier alias="identifier"]
     }
private import_recover ::= !('$expand' | 'function' | 'import' | 'static' | 'zenClass' | ';' | statement_start)

scriptBody ::= topLevelStatement*
    {
        pin=1; implements="ZenScriptCodeBlock"; mixin="ZenScriptCodeBlockImpl";
        methods=[statements="statement" functions="functionDeclaration" classes="classDeclaration" expandFunctions="expandFunctionDeclaration"]
    }

private topLevelStatement ::=  functionDeclaration | expandFunctionDeclaration | classDeclaration | statement {  recoverWhile=script_body_recover; }

private script_body_recover ::= !('$expand' | 'function' | 'static' | 'zenClass' | statement_start)

private funcReturnTypeDef ::= 'as' typeRef { pin=1 }
functionDeclaration ::=
    ('static')? 'function' !'(' identifier parameters (funcReturnTypeDef | &'{') functionBody
    { pin=3; implements=["ZenScriptNamedElement" "ZenScriptMember"];
      mixin="ZenScriptNamedElementImpl"; methods=[returnType="typeRef"];}

expandFunctionDeclaration ::= '$expand' typeRef '$' identifier parameters (funcReturnTypeDef | &'{') functionBody
    { pin=1; implements=["ZenScriptNamedElement" "ZenScriptMember"];
      mixin="ZenScriptNamedElementImpl"; methods=[returnType="typeRef[1]" expandTarget="typeRef[0]"];}

functionBody ::= '{' statement* '}'
    { pin=1;  implements="ZenScriptCodeBlock"; mixin="ZenScriptCodeBlockImpl"; methods=[statements="statement"];}

classDeclaration ::= 'zenClass' qualifiedName '{' classBodyItem* '}'
    { pin=1; implements="ZenScriptClass"; mixin="ZenScriptClassImpl"; }

private classBodyItem ::= variableDeclaration | constructorDeclaration | functionDeclaration { pin=1;  recoverWhile=class_body_recover; }
private class_body_recover ::= !('}' | 'function' | 'global' | 'static' | 'val' | 'var' | 'zenConstructor')

constructorDeclaration ::= 'zenConstructor' parameters functionBody
    { pin=1; methods=[getReturnType getIdentifier]; implements=[ "ZenScriptMember" ] mixin="ZenScriptNamedElementImpl"; }

variableDeclaration ::=
    ('var' | 'val' | 'static' | 'global') identifier ('as' typeRef)? ('=' initializerOrDefault)? ';'
    { pin(".*")=1; extends="statement";
      methods=[initializer="initializerOrDefault"];
      implements=["ZenScriptMember" "ZenScriptNamedElement"]; mixin="ZenScriptNamedElementImpl"; }

initializerOrDefault ::= expression

qualifiedName ::= qualifier? identifier { pin=1 }
qualifier ::= (identifier '.')+ { pin(".*")=2 }

parameters ::='(' (!')' parametersList)? ')' { pin(".*")=1 }
private parametersList ::= parameter (',' parameter)*  { pin(".*")=1 }
parameter ::= identifier ('as' typeRef)? ('=' initializerOrDefault)?
    {
        pin(".*")=1; recoverWhile=parameter_recover;
        methods=[initializer="initializerOrDefault"];
        implements="ZenScriptNamedElement";
        mixin="ZenScriptNamedElementImpl";
    }
private parameter_recover ::= !(',' | ')' | 'as' | '{' | '}') & script_body_recover

private statement_start ::= 'break' | 'continue' | 'for' | 'global' | 'if' | 'return' | 'static' | 'val' | 'var' | 'while' | '{' | ';' | expression_start

statement ::=
      blockStatement
    | returnStatement
    | breakStatement
    | continueStatement
    | ifStatement
    | foreachStatement
    | whileStatement
    | variableDeclaration
    | expressionStatement
    | ';'
    { pin = 1; recoverWhile=statement_recover; }

private statement_recover ::= !('}' | 'else' | ';' | '$expand' | 'function' | 'static' | 'zenClass' | statement_start)

blockStatement ::= '{' statement* '}' { pin=1; implements="ZenScriptCodeBlock"; mixin="ZenScriptCodeBlockImpl"; methods=[statements="statement"]; }

returnStatement ::= 'return' expression? ';' { pin=1 }

breakStatement ::= 'break' ';' { pin=1 }

continueStatement ::= 'continue' ';' { pin=1 }

ifStatement ::= 'if' expression statement ('else' statement)? { pin(".*")=1; methods=[
        condition="expression"
        thenBody="statement[0]"
        elseBody="statement[1]"
    ];}

foreachStatement ::=
    'for' foreachEntry 'in' expression statement
    { pin=1; methods=[
            entries="foreachVariableDeclaration"
            iterTarget="expression"
            body="statement"
            processDeclarations
        ];
     implements="ZenScriptLoopStatement"
    }

private foreachEntry ::= foreachVariableDeclaration (',' foreachVariableDeclaration)* { pin(".*")=1; recoverWhile=for_each_entry_recover }

private for_each_entry_recover ::= !(',' | 'in' | '{' | ';')

foreachVariableDeclaration ::= identifier
    {  implements="ZenScriptNamedElement"; mixin="ZenScriptNamedElementImpl";  }

whileStatement ::=
    'while' expression statement
    { pin=1; methods=[condition="expression" body="statement"];
      implements="ZenScriptLoopStatement" }

expressionStatement ::=
    expression ';' { pin=1 }

private expression_start ::= '!' | '(' | '-' | '<' | '[' | 'any' | 'bool' | 'byte' | 'double' | 'false' | 'float' | 'function' | 'int' | 'long' | 'null' | 'short' | 'string' | 'to' | 'true' | 'void' | '{' | DOUBLE_LITERAL | FLOAT_LITERAL | ID | INT_LITERAL | LONG_LITERAL | STRING_LITERAL

expression ::=
      assignmentExpression
    | conditionalExpression
    | orOrExpression
    | andAndExpression
    | orExpression
    | xorExpression
    | andExpression
    | compareExpression
    | addExpression
    | mulExpression
    | unaryExpression
    | rangeExpression
    | postfixExpression
    { pin=1; recoverWhile=expression_recover; }

private rangeOp ::= 'to' | '..'
private unaryOp ::= '!' | '-'
private mulOp ::= '*' | '/' | '%'
private addOp ::= '+' | '-' | '~'
private compareOP ::= '==' | '!=' | '<' | '<=' | '>' | '>=' | 'in' | 'has'

private assignmentOp ::= '=' | '+=' | '-=' | '*=' | '/=' | '%=' | '~=' | '&=' | '|=' | '^='


private binaryOP ::= rangeOp | mulOp |  addOp | compareOP | assignmentOp | '&' | '^' | '|' |  '&&' |  '||'
private allOP ::= binaryOP | unaryOp | ':' | '.' '?'

private expression_recover ::= !(allOP | statement_start | ')' | ',' | ':' | ';'| ']' | 'as' | 'else' | 'instanceof' | '}')

fake binaryExpression ::= expression
    binaryOP
    expression { pin=2;  methods=[
        left="expression[0]"
        right="expression[1]"
        getOperator
    ]}

rangeExpression ::= expression rangeOp expression
unaryExpression ::= unaryOp expression { rightAssociative = true; pin=1; methods=[getOperator]; }
mulExpression ::= expression mulOp expression
addExpression ::= expression addOp expression
compareExpression ::= expression compareOP expression  { rightAssociative = true }
andExpression ::= expression '&' expression
xorExpression ::= expression '^' expression
orExpression ::= expression '|' expression
andAndExpression ::= expression '&&' expression
orOrExpression ::= expression '||' expression
conditionalExpression ::= expression conditionalTail
    { rightAssociative = true;  methods=[
        condition="expression[0]"
        truePart="expression[1]"
        falsePart="expression[2]"
    ]}
assignmentExpression ::= expression assignmentOp expression  { rightAssociative = true }
parenExpression ::= '(' expression ')' { pin=1 }

private conditionalTail ::= '?' expression ':' expression { pin=1 }

memberName ::= identifier | 'string' { extends="identifier"; }

left memberAccessExpression ::= '.' memberName
    { pin=1; implements="ZenScriptReference"; mixin="ZenScriptReferenceImpl"; methods=[getRangeInElement] }
left arrayIndexExpression ::= '[' expression ']'
    { pin=1; methods=[
        expression='expression[0]'
        index='expression[1]'
    ] }
left callExpression ::= arguments
left typeCastExpression ::= 'as' typeRef { pin=1 }
left instanceOfExpression ::= 'instanceof' typeRef { pin=1 }


private primaryExpression ::= literalExpression | localAccessExpression | parenExpression { pin=1 }
postfixExpression ::=  primaryExpression (
      memberAccessExpression
    | arrayIndexExpression
    | callExpression
    | typeCastExpression
    | instanceOfExpression
)* { pin=1 }



localAccessExpression ::= identifier | primitiveTypeRef
    { implements="ZenScriptReference"; mixin="ZenScriptReferenceImpl"; }


literalExpression ::= arrayLiteral | mapLiteral | functionLiteral | bracketHandlerLiteral | primitiveLiteral


arrayLiteral ::= '[' (!']' expression (',' (expression | &']'))*)? ']' { pin(".*")=1 }
mapLiteral ::= '{' (!'}' mapEntry (',' (mapEntry | &'}'))*)? '}' { pin(".*")=1 }
mapEntry ::= expression ':' expression
    { pin=1; methods=[key="expression[0]" value="expression[1]"]; recoverWhile=map_literal_recover; }

private map_literal_recover ::= !(',' | '}') & statement_recover

functionLiteral ::= 'function' parameters funcReturnTypeDef? functionBody
    { pin=1;  methods=[returnType="typeRef"]; }

primitiveLiteral ::=
      INT_LITERAL
    | LONG_LITERAL
    | FLOAT_LITERAL
    | DOUBLE_LITERAL
    | STRING_LITERAL
    | 'true'
    | 'false'
    | 'null'

bracketHandlerLiteral ::= '<' (!'>' <<any>>)+ '>' { pin(".*")=1; }

arguments ::= '(' argumentList? ')' { pin=1 }
private argumentList ::= expression (',' expression)* { pin(".*")="','" }


classTypeRef ::= &identifier qualifiedName
    { pin=1; implements="ZenScriptReference"; mixin="ZenScriptReferenceImpl"; }

typeRef ::=
    primitiveTypeRef
  | arrayOrMap
  | listTypeRef
  | classTypeRef
  | functionTypeRef
  { recoverWhile=type_recover; }

functionTypeRef ::= 'function' '(' functionParamsTypeRef? ')' typeRef
    { rightAssociative=true; pin=1; methods=[getReturnType getParamsType] }
private functionParamsTypeRef ::= typeRef (',' typeRef)* { pin(".*")=1 }
listTypeRef ::= '[' typeRef ']' { pin=1 }

private type_recover ::= !('$' | '=' | '[' | ']' | ',' | 'to' | ID | 'function' | primitiveTypeRef) & expression_recover & statement_recover

private arrayOrMap ::=  arrayTypeRef | mapTypeRef
arrayTypeRef ::= typeRef '[' ']'
mapTypeRef ::= typeRef '[' typeRef ']'
    { rightAssociative=true; methods=[keyType="typeRef[1]" valueType="typeRef[0]"] }


primitiveTypeRef ::=
    'any'
  | 'byte'
  | 'short'
  | 'int'
  | 'long'
  | 'float'
  | 'double'
  | 'bool'
  | 'void'
  | 'string'

identifier ::= ID | 'to'